buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java'
    id 'eclipse'
    id 'application'
    id 'maven-publish'
    id 'com.jfrog.bintray' version "1.8.4"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

group = 'de.cronn'

version = '1.5'

executableDir = "scripts"

startScripts {
    mainClassName = 'de.cronn.diff.Main' 
    applicationName = project.name
}

repositories {
    mavenCentral()
}

eclipse {
    classpath {
       downloadSources=true
    }
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
            srcDir 'src/main/resources'
        }
    }
    test {
        java {
            srcDir 'src/test/java'
        }
    }
}

test {
    systemProperty "file.encoding", "utf-8"
}

test.inputs.dir('data/test/input')
test.inputs.dir('data/test/validation')
test.outputs.dir('data/test/output')

jar {
    manifest {
        attributes 'Main-Class': 'de.cronn.diff.Main'
    }
 
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
            }
        }
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }

    developers {
        developer {
            id "maurice.velte"
            name "Maurice Velte"
            email "maurice.velte@cronn.de"
        }
    }

    scm {
        url "https://github.com/cronn-de/diff-to-html"
    }
}

publishing {
    publications {
        CronnPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId project.name
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Display diffs in a convenient html page')
                root.appendNode('name', project.name)
                root.appendNode('url', 'https://github.com/cronn-de/diff-to-html')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    if (user == null) println "Please set env var BINTRAY_USER"
    if (key == null) println "Please set env var BINTRAY_API_KEY"

    publications = ['CronnPublication']
    dryRun = false
    pkg {
        repo = 'maven'
        name = project.name
        userOrg = 'cronn'
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/cronn-de/diff-to-html'
        issueTrackerUrl = 'https://github.com/cronn-de/diff-to-html/issues'
        vcsUrl = 'https://github.com/cronn-de/diff-to-html.git'
        version {
            name = project.version
            released = new Date()
        }
    }
    override = true
}

ext {
    junitVersion_4 = '4.11'
    apacheCommonsTextVersion = '1.2'
    apacheCommonsIOVersion = '2.6'
    commonsCliVersion = '1.3'
    j2htmlVersion = '1.2.2'
    // javaDiffUtilsVersion = '4.0'
    juniversalchardetVersion = '1.0.3'
    systemRulesVersion = '1.16.0'
    assertJVersion = "3.12.2"
}

dependencies {
        compile("org.apache.commons:commons-text:${apacheCommonsTextVersion}")
        compile("commons-cli:commons-cli:${commonsCliVersion}")
        compile("commons-io:commons-io:${apacheCommonsIOVersion}")
        compile("com.j2html:j2html:${j2htmlVersion}")
        // compile("io.github.java-diff-utils:java-diff-utils:${javaDiffUtilsVersion}")
        compile files('libs/java-diff-utils-4.1-SNAPSHOT.jar')
        compile("com.googlecode.juniversalchardet:juniversalchardet:${juniversalchardetVersion}")
        testCompile("junit:junit:${junitVersion_4}")
        testCompile("com.github.stefanbirkner:system-rules:${systemRulesVersion}")
        testCompile("org.assertj:assertj-core:${assertJVersion}")
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
